cmake_minimum_required(VERSION 3.6)
project(TwoLAME LANGUAGES C)

add_subdirectory(libtwolame)

option(EXECUTABLE "Build twolame executable in addition to libtwolame library." ON)
if(EXECUTABLE)
  find_package(PkgConfig)
  if(PkgConfig_FOUND)
    pkg_check_modules(SndFile sndfile IMPORTED_TARGET)
    if(SndFile_FOUND AND NOT TARGET SndFile::SndFile)
      add_library(SndFile::SndFile INTERFACE IMPORTED)
      target_link_libraries(SndFile::SndFile INTERFACE PkgConfig::SndFile)
    endif()
  else()
    find_library(SndFile_LINK_LIBRARIES NAMES sndfile sndfile-1)
    find_file(SndFile_INCLUDEDIR NAMES sndfile.h)

    if(SndFile_LINK_LIBRARIES AND SndFile_INCLUDEDIR AND NOT TARGET SndFile::SndFile)
      add_library(SndFile::SndFile INTERFACE IMPORTED)
      target_link_libraries(SndFile::SndFile INTERFACE SndFile_LINK_LIBRARIES)
      target_include_directories(SndFile::SndFile INTERFACE SndFile_INCLUDEDIR)
      set(SndFile_FOUND TRUE)
    endif()
  endif()

  if(SndFile_FOUND)
    message(STATUS "libsndfile found, building twolame executable.")
    add_subdirectory(frontend)
  else()
    message(STATUS "libsndfile not found, building simple twolame executable.")
    add_subdirectory(simplefrontend)
  endif()
endif()
